# Provides rage decryption and other unsafe builtins using builtins.exec.
# Requires `allow-unsafe-native-code-during-evaluation = true` in nix config.
let
  assertMsg = pred: msg: pred || builtins.throw msg;
  hasSuffix =
    suffix: content:
    let
      lenContent = builtins.stringLength content;
      lenSuffix = builtins.stringLength suffix;
    in
    lenContent >= lenSuffix && builtins.substring (lenContent - lenSuffix) lenContent content == suffix;
in
{
  # Instead of calling rage directly here, we call a wrapper script that will cache the output
  # in a predictable path in /tmp, which allows us to only require the password for each encrypted
  # file once.
  rageImportEncrypted =
    identities: nixFile:
    assert assertMsg (builtins.isPath nixFile)
      "The file to decrypt must be given as a path to prevent impurity.";
    assert assertMsg (hasSuffix ".nix.age" nixFile)
      "The content of the decrypted file must be a nix expression and should therefore end in .nix.age";
    builtins.exec (
      [
        ./rage-decrypt-and-cache.sh
        nixFile
      ]
      ++ identities
    );
  # currentSystem
  unsafeCurrentSystem = builtins.exec [
    "nix"
    "eval"
    "--impure"
    "--expr"
    "builtins.currentSystem"
  ];
}
